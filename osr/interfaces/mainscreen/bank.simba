(*
# Bank
Methods to extend and/or modify SRL's {ref}`Bank` interface.
*)
{$DEFINE WL_BANK_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}


function TRSBank.FindItem(item: TRSItem; out box: TBox): Boolean; override;
begin
  if not Self.IsOpen() then
    Exit;

  Result := Self.Items.Find(item.GetArray(), box);
end;

function TRSBank.ContainsItem(item: TRSItem): Boolean; override;
begin
  Result := Self.Items.ContainsAny(item.GetArray());
end;


function TRSBank.ContainsItem(item: TRSBankWithdrawItem): Boolean; overload; deprecated 'Use the TRSBankItem version instead!';
begin
  Result := Self.ContainsItem(item.Item);
end;

function TRSBank.FindItem(bankTab: Int32; item: TRSItem; out Bounds: TBox): Boolean; overload;
begin
  if Self.OpenTab(bankTab) then
    Result := Self.FindItem(item, Bounds);
end;

function TRSBank.ContainsItem(bankTab: Int32; Item: TRSItem): Boolean; overload;
begin
  if Self.OpenTab(bankTab) then
    Result := Self.ContainsItem(item);
end;

function TRSBank.ContainsItem(bankTab: Int32; item: TRSBankWithdrawItem): Boolean; overload; deprecated 'Use the TRSBankItem version instead!';
var
  bankItem: TRSBankItem;
begin
  bankItem := TRSBankItem.Setup(item.item, item.Quantity, item.Noted);
  bankItem.Tab := bankTab;
  Result := Self.ContainsItem(bankItem);
end;



function TRSBank.DepositAll(): Boolean; override;
var
  buttons: TRSButtonArray;
  i, count: Int32;
begin
  if Inventory.Count() = 0 then
    Exit(True);

  buttons := Self.GetButtons();

  for i := 0 to High(buttons) do
  begin
    count := SRL.CountColor(CTS2(1717089, 13, 0.24, 1.71), buttons[i].Bounds);
    if count > 180 then
      Break;
  end;

  if (i > High(Buttons)) and Bank.IsOpen() then
    Self.Fatal('This script needs the "Deposit Inventory" button visible!');

  Result := buttons[i].Click();
end;


function TRSBank.WithdrawSlot(tab: Int32; item: TRSBankWithdrawSlot; useQuantityButtons: Boolean): Boolean; overload;
var
  itemCount: Int32;
begin
  if not Self.IsOpen or (tab = -1) then
    Exit;

  Self.OpenTab(tab);

  itemCount := Self.CountSlotStack(item.Slot);
  if Self.WithdrawSlot(item, useQuantityButtons) then
    Result := WaitUntil(Self.CountSlotStack(item.Slot) < itemCount, 100, 2000);
end;


function TRSBank.WithdrawItem(out tab: Int32; item: TRSBankWithdrawItem; useQuantityButtons: Boolean): Boolean; overload; deprecated 'Use TRSBankItem version instead!';
var
  b: TBox;
  count: Int32;
begin
  if not Self.IsOpen() then
    Exit;

  if Inventory.IsFull() and (Length(Item.Item.GetArray()) = 1) and not Inventory.ItemIsStackable(Item.Item) then
    Exit;

  if not Self.FindItem(item.Item, b) then
  begin
    if tab = -1 then
      tab := Self.FindItemTab(item.Item)
    else
      Self.OpenTab(tab);

    if not Self.FindItem(item.Item, b) then
      Exit(False);
  end;

  count := SRL.GetItemAmount(b);
  if Self.WithdrawHelper(b, item.Quantity, item.Noted, useQuantityButtons) then
    Result := WaitUntil(SRL.GetItemAmount(b) < count, 100, 2000);
end;

function TRSBank.WithdrawItems(tab: Int32; items: array of TRSBankWithdrawItem; UseQuantityButtons: Boolean): Boolean; deprecated 'Use TRSBankItem version instead!';
var
  item: TRSBankWithdrawItem;
  bankItem: TRSBankItem;
begin
  for item in items do
  begin
    bankItem := TRSBankItem.Setup(item.Item, item.Quantity, item.Noted);
    bankItem.Tab := tab;
    if not Self.WithdrawItem(bankItem, UseQuantityButtons) then
      Exit(False);
  end;
  Result := True;
end;



function TRSBank.DepositAllBut(Tab: Int32; Items: TRSItemArray): Boolean; deprecated 'Use Bank.DepositRandomItems() instead.';
var
  Item: TRSItem;
  Slots: TIntegerArray;
  DepositSlots: TIntegerArray;
begin
  if not Self.IsOpen then
    Exit;

  if Tab > -1 then
    Self.OpenTab(Tab);

  for Item in Items do
    Inventory.FindItem(Item, Slots);

  DepositSlots := Slots.Difference(Inventory.GetUsedSlots);

  if DepositSlots <> [] then
  repeat
    if not Self.IsOpen then //Exit in case the bank somehow closed.
      Exit;

    if Self.DepositSlot([DepositSlots[0], Self.QUANTITY_ALL], True) then
      WaitUntil(not Inventory.IsSlotUsed(DepositSlots[0]), 100, 2000);

    DepositSlots := Slots.Difference(Inventory.GetUsedSlots);
  until Result := (DepositSlots = []);
end;

function TRSBank.DepositRandomItems(items: TRSItemArray): Boolean;
var
  slots: TIntegerArray;
  t: UInt64;
begin
  slots := Inventory.FindRandomItems(items);
  t := GetTickCount() + 50000;

  while slots <> [] do
  begin
    if GetTickCount() >= t then
      Exit;
    if not Self.IsOpen() then //Exit in case the bank somehow closed.
      Exit;

    if Self.DepositSlot([slots[0], Bank.QUANTITY_ALL], True) then
      WaitUntil(not Inventory.IsSlotUsed(slots[0]), 100, 2000);

    slots := Inventory.FindRandomItems(items);
  end;

  Result := True;
end;


function TRSBank.Close(pressEscape: Boolean = False): Boolean; override;
begin
  if not Self.IsOpen(False) then
    Exit(True);

  Result := RSInterface.Close(pressEscape);
end;

function TRSBank.Close(chance: Double): Boolean; overload;
var
  pressEscape: Boolean;
begin
  if chance = BioHash then
    pressEscape := Antiban.BioDice()
  else
    pressEscape := SRL.Dice(chance);

  Result := RSInterface.Close(pressEscape);
end;

//Temporarily here.
function TRSDepositBox.Close(PressEscape: Boolean = False): Boolean; override;
begin
  if not Self.IsOpen then
    Exit(True);

  Result := RSInterface.Close(PressEscape);
end;

//Temporarily here.
function TRSGrandExchange.Close(PressEscape: Boolean = False): Boolean; override;
begin
  if not Self.IsOpen then
    Exit(True);

  Result := RSInterface.Close(PressEscape);
end;

