(*
BankPin
=======
Methods to extend and/or modify SRL's bank pin interface.
*)

{$DEFINE WL_BANKPIN_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

(*
BankPin.ClickDigit
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSBankPin.ClickDigit(Digit: Char): Boolean; override;

Overrides TRSBankPin.ClickDigit with INeedBot's bounds fixes.
*)
function TRSBankPin.ClickDigit(Digit: Char): Boolean; override;
var
  buttons: TRSButtonArray;
  hovering: Boolean;
  i: Int32;
begin
  if not Self.IsOpen() then
    Exit;

  buttons := Self.GetButtons();

  if Length(buttons) <> 10 then
    Exit;

  for i := 0 to High(buttons) do
  begin
    if OCR.LocateText(buttons[i].Bounds.Expand(5), Digit, RS_FONT_BOLD_12, TOCRColorFilter.Create([$007FFF]), 1) then
    begin
      Mouse.Click(buttons[i].Bounds, MOUSE_LEFT);
      Exit(True);
    end;

    if buttons[i].Bounds.Expand(5).Contains(Mouse.Position()) then
      hovering := True;
  end;


  if hovering then
  begin
    Mouse.Click(MOUSE_LEFT);
    Exit(True);
  end;
end;

(*
BankPin.Enter
~~~~~~~~~~~~~
.. pascal:: function TRSBankPin.Enter(pin: String): Boolean; override;

Override to make the method check for failed pin in the chatbox and handle that.
*)
function TRSBankPin.Enter(pin: String): Boolean; override;

  function GetDigitToEnter(): Char;
  begin
    if OCR.LocateText(Self.Bounds, 'FIRST',  RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[1]);
    if OCR.LocateText(Self.Bounds, 'SECOND', RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[2]);
    if OCR.LocateText(Self.Bounds, 'THIRD',  RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[3]);
    if OCR.LocateText(Self.Bounds, 'FOURTH', RS_FONT_BOLD_12, TOCRColorFilter.Create([$FFFFFF]), 1) then Exit(pin[4]);
  end;

var
  i, j: Int32;
  digit: Char;
begin
  for i := 1 to 3 do // 3 Attempts
  begin
    for j := 1 to 4 do
    begin
      digit := GetDigitToEnter();
      if digit = '' then
        Break;

      if not Self.ClickDigit(digit) then
        Break;

      Wait(1000, 2000, wdLeft);
    end;

    if 'try again' in Chat.GetChat() then
    begin
      Chat.ClickContinue(True);
      Continue;
    end;

    if Bank.IsOpen(3000) then
      Exit(True);
  end;

  Result := Bank.IsOpen(3000); // wait 3 extra seconds before failing.
end;

