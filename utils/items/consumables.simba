(*
Consumables
===========
This file contains types and arrays of consumable items that can be used as is or by the consumable handlers in wasplib.
*)
{$DEFINE WL_CONSUMABLES_INCLUDED}
{$IFNDEF WL_UTILS}
{$I WaspLib/utils.simba}
{$ENDIF}

type

(*
type ERSConsumable
~~~~~~~~~~~~~~~~~~
.. pascal::
  ERSConsumable = (
    FOOD_CONSUMABLE,
    PRAYER_CONSUMABLE,
    ENERGY_CONSUMABLE,
    POISON_CONSUMABLE,
    VENOM_CONSUMABLE,
    ANTI_FIRE_CONSUMABLE,
    BOOST_CONSUMABLE,
    STRENGTH_BOOST_CONSUMABLE,
    ATTACK_BOOST_CONSUMABLE,
    DEFENCE_BOOST_CONSUMABLE,
    RANGING_BOOST_CONSUMABLE,
    MAGIC_BOOST_CONSUMABLE
  );

ERSConsumable is a enumerator that contains all types of consumable.
*)
  ERSConsumable = (FOOD_CONSUMABLE, PRAYER_CONSUMABLE, ENERGY_CONSUMABLE, POISON_CONSUMABLE, VENOM_CONSUMABLE, ANTI_FIRE_CONSUMABLE, BOOST_CONSUMABLE, STRENGTH_BOOST_CONSUMABLE, ATTACK_BOOST_CONSUMABLE, DEFENCE_BOOST_CONSUMABLE, RANGING_BOOST_CONSUMABLE, MAGIC_BOOST_CONSUMABLE);

const
(*
const CONSUMABLE_ARRAYS
~~~~~~~~~~~~~~~~~~~~~~~
.. pascal::
  const
    FOOD_ARRAY: TRSItemArray = ['Shrimps', 'Cooked chicken', 'Cooked meat', 'Sardine', 'Bread', 'Herring', 'Mackerel', 'Choc-ice', 'Trout', 'Cod', 'Pike', 'Roast beast meat', 'Pineapple punch', 'Salmon', 'Tuna', 'Jug of wine', 'Rainbow fish', 'Stew', 'Banana stew', 'Cake(1..3)', 'Meat pie(1..2)', 'Bass', 'Plain pizza(1..2)', 'Lobster', 'Swordfish', 'Potato with butter', 'Apple pie(1..2)', 'Chocolate cake(1..3)', 'Tangled toad' 's legs', 'Chocolate bomb', 'Potato with cheese', 'Meat pizza(1..2)', 'Admiral pie(1..2)', 'Monkfish', 'Anchovy pizza(1..2)', 'Cooked karambwan', 'Curry', 'Ugthanki kebab', 'Guthix rest(1..4)', 'Dragonfruit pie(1..2)', 'Mushroom potato', 'Shark', 'Sea turtle', 'Pineapple pizza(1..2)', 'Summer pie(1..2)', 'Wild pie(1..2)', 'Manta ray', 'Tuna potato', 'Dark crab', 'Anglerfish', 'Saradomin brew(1..4)'];
    PRAYER_ARRAY: TRSItemArray = ['Zamorak brew(1..4)', 'Sanfew serum(1..4)', 'Super restore(1..4)', 'Prayer potion(1..4)', 'Jangerberries'];
    ENERGY_ARRAY: TRSItemArray = ['White tree fruit', 'Winter sq' 'irkjuice', 'Spring sq' 'irkjuice', 'Autumn sq' 'irkjuice', 'Summer sq' 'irkjuice', 'Bandages', 'Guthix rest(1..4)', 'Papaya fruit', 'Energy potion(1..4)', 'Purple sweets', 'Summer pie(1..2)', 'Super energy(1..4)', 'Stamina potion(1..4)', 'Strange fruit', 'Mint cake', 'Gout tuber'];
    ANTI_POISON_ARRAY: TRSItemArray = ['Sanfew serum(1..4)', 'Anti-venom+(1..4)', 'Anti-venom(1..4)', 'Antidote++(1..4)', 'Antidote+(1..4)', 'Superantipoison(1..4)', 'Antipoison(1..4)'];
    ANTI_VENOM_ARRAY: TRSItemArray = ['Anti-venom+(1..4)', 'Anti-venom(1..4)'];
    ANTI_FIRE_ARRAY: TRSItemArray = ['Antifire potion(1..4)', 'Super antifire potion(1..4)', 'Extended antifire(1..4)', 'Extended super antifire(1..4)'];
    BOOST_ARRAY: TRSItemArray = ['Divine super combat potion(1..4)', 'Super combat potion(1..4)', 'Divine bastion potion(1..4)', 'Bastion potion(1..4)', 'Divine battlemage potion(1..4)', 'Battlemage potion(1..4)'];
    STRENGTH_BOOST_ARRAY: TRSItemArray = ['Divine super strength potion(1..4)', 'Super strength(1..4)', 'Strength potion(1..4)'];
    ATTACK_BOOST_ARRAY: TRSItemArray = ['Divine super attack potion(1..4)', 'Super attack(1..4)', 'Attack potion(1..4)'];
    DEFENCE_BOOST_ARRAY: TRSItemArray = ['Divine super defence potion(1..4)', 'Super defence(1..4)', 'Defence potion(1..4)'];
    RANGING_BOOST_ARRAY: TRSItemArray = ['Divine ranging potion(1..4)', 'Ranging potion(1..4)'];
    MAGIC_BOOST_ARRAY: TRSItemArray = ['Divine magic potion(1..4)', 'Magic potion(1..4)'];
    TRASH_ARRAY: TRSItemArray = ['Cocktail glass', 'Jug', 'Bowl', 'Pie dish', 'Vial', 'Beer glass', 'Empty cup'];

Global consumable arrays variables.
These arrays holds all types of consumable for each respective purpose with the exception of TRASH_ARRAY which are possible left over items from consumables.
They are in UPPERCASE because they are intended to be treated as constants but are variables in case the user whishes to modify them.
*)
  FOOD_ARRAY: TRSItemArray = ['Shrimps', 'Cooked chicken', 'Cooked meat', 'Sardine', 'Bread', 'Herring', 'Mackerel', 'Choc-ice', 'Trout', 'Cod', 'Pike', 'Roast beast meat', 'Pineapple punch', 'Salmon', 'Tuna', 'Jug of wine', 'Rainbow fish', 'Stew', 'Banana stew', 'Cake(1..3)', 'Meat pie(1..2)', 'Bass', 'Plain pizza(1..2)', 'Lobster', 'Swordfish', 'Potato with butter', 'Apple pie(1..2)', 'Chocolate cake(1..3)', 'Tangled toad' 's legs', 'Chocolate bomb', 'Potato with cheese', 'Meat pizza(1..2)', 'Admiral pie(1..2)', 'Monkfish', 'Anchovy pizza(1..2)', 'Cooked karambwan', 'Curry', 'Ugthanki kebab', 'Guthix rest(1..4)', 'Dragonfruit pie(1..2)', 'Mushroom potato', 'Shark', 'Sea turtle', 'Pineapple pizza(1..2)', 'Summer pie(1..2)', 'Wild pie(1..2)', 'Manta ray', 'Tuna potato', 'Dark crab', 'Anglerfish', 'Saradomin brew(1..4)'];
  PRAYER_ARRAY: TRSItemArray = ['Zamorak brew(1..4)', 'Sanfew serum(1..4)', 'Super restore(1..4)', 'Prayer potion(1..4)', 'Jangerberries'];
  ENERGY_ARRAY: TRSItemArray = ['White tree fruit', 'Winter sq' 'irkjuice', 'Spring sq' 'irkjuice', 'Autumn sq' 'irkjuice', 'Summer sq' 'irkjuice', 'Bandages', 'Guthix rest(1..4)', 'Papaya fruit', 'Energy potion(1..4)', 'Purple sweets', 'Summer pie(1..2)', 'Super energy(1..4)', 'Stamina potion(1..4)', 'Strange fruit', 'Mint cake', 'Gout tuber'];
  ANTI_POISON_ARRAY: TRSItemArray = ['Sanfew serum(1..4)', 'Anti-venom+(1..4)', 'Anti-venom(1..4)', 'Antidote++(1..4)', 'Antidote+(1..4)', 'Superantipoison(1..4)', 'Antipoison(1..4)'];
  ANTI_VENOM_ARRAY: TRSItemArray = ['Anti-venom+(1..4)', 'Anti-venom(1..4)'];
  ANTI_FIRE_ARRAY: TRSItemArray = ['Antifire potion(1..4)', 'Super antifire potion(1..4)', 'Extended antifire(1..4)', 'Extended super antifire(1..4)'];
  BOOST_ARRAY: TRSItemArray = ['Divine super combat potion(1..4)', 'Super combat potion(1..4)', 'Divine bastion potion(1..4)', 'Bastion potion(1..4)', 'Divine battlemage potion(1..4)', 'Battlemage potion(1..4)'];
  STRENGTH_BOOST_ARRAY: TRSItemArray = ['Divine super strength potion(1..4)', 'Super strength(1..4)', 'Strength potion(1..4)'];
  ATTACK_BOOST_ARRAY: TRSItemArray = ['Divine super attack potion(1..4)', 'Super attack(1..4)', 'Attack potion(1..4)'];
  DEFENCE_BOOST_ARRAY: TRSItemArray = ['Divine super defence potion(1..4)', 'Super defence(1..4)', 'Defence potion(1..4)'];
  RANGING_BOOST_ARRAY: TRSItemArray = ['Divine ranging potion(1..4)', 'Ranging potion(1..4)'];
  MAGIC_BOOST_ARRAY: TRSItemArray = ['Divine magic potion(1..4)', 'Magic potion(1..4)'];
  TRASH_ARRAY: TRSItemArray = ['Cocktail glass', 'Jug', 'Bowl', 'Pie dish', 'Vial', 'Beer glass', 'Empty cup'];

const
(*
const CONSUMABLE_ARRAYS
~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: CONSUMABLE_ARRAYS: array [ERSConsumable] of TRSItemArray = [FOOD_ARRAY, PRAYER_ARRAY, ENERGY_ARRAY, ANTI_POISON_ARRAY, ANTI_VENOM_ARRAY, ANTI_FIRE_ARRAY, BOOST_ARRAY, STRENGTH_BOOST_ARRAY, ATTACK_BOOST_ARRAY, DEFENCE_BOOST_ARRAY, RANGING_BOOST_ARRAY, MAGIC_BOOST_ARRAY];

Global array of all consumables. One can access a consumable array type easiely like so:

Example
-------

  WriteLn CONSUMABLE_ARRAYS[ERSConsumable.ENERGY_CONSUMABLE];
*)
  CONSUMABLE_ARRAYS: array [ERSConsumable] of TRSItemArray = [FOOD_ARRAY, PRAYER_ARRAY, ENERGY_ARRAY, ANTI_POISON_ARRAY, ANTI_VENOM_ARRAY, ANTI_FIRE_ARRAY, BOOST_ARRAY, STRENGTH_BOOST_ARRAY, ATTACK_BOOST_ARRAY, DEFENCE_BOOST_ARRAY, RANGING_BOOST_ARRAY, MAGIC_BOOST_ARRAY];

type
(*
type TRSConsumable
~~~~~~~~~~~~~~~~~~
.. pascal::
  TRSConsumable = record
    Item: TRSItem;
    Points: UInt32;
    Timer: UInt32;
    BankTab: Int32;
    Cost: Int32;
    IsSetup: Boolean;
  end;
  TRSConsumableArray = array of TRSConsumable;

TRSConsumable is the record used in all consumables. It contains base properties all consumables have.
*)
  TRSConsumable = record
    Item: TRSItem;
    BankItem: TRSBankItem;
    Points: UInt32;
    Timer: UInt32;
    Cost: Int32;
    IsSetup: Boolean;
  end;
  TRSConsumableArray = array of TRSConsumable;

(*
type TRSConsumableType
~~~~~~~~~~~~~~~~~~~~~~
.. pascal::
  type
    TRSFood = type TRSConsumable;
    TRSFoodArray = array of TRSFood;
    TRSPrayerPotion = type TRSConsumable;
    TRSPrayerPotionArray = array of TRSPrayerPotion;
    TRSEnergyBoost = type TRSConsumable;
    TRSEnergyBoostArray = array of TRSEnergyBoost;
    TRSBoost = record(TRSConsumable)
      Skills: array of ERSSkill;
      LevelsBoost: TIntegerArray;
      Countdown: TCountdown;
    end;
    TRSBoosArray = array of TRSBoost;

Types used in all consumables for each respective purpose. This includes food and certain potions.
TRSBoost type of consumable has a timer to keep track when it lost it's effect after being eaten/drank.
*)
  TRSFood = type TRSConsumable;
  TRSFoodArray = array of TRSFood;

  TRSPrayerPotion = type TRSConsumable;
  TRSPrayerPotionArray = array of TRSPrayerPotion;

  TRSEnergyBoost = type TRSConsumable;
  TRSEnergyBoostArray = array of TRSEnergyBoost;

  TRSBoost = record(TRSConsumable)
    Skills: array of ERSSkill;
    LevelsBoost: TIntegerArray;
    Countdown: TCountdown;
  end;
  TRSBoosArray = array of TRSBoost;

(*
Consumable.Setup
~~~~~~~~~~~~~~~~
.. pascal:: procedure TRSConsumable.Setup();

Used internally by consumable handlers/managers to setup common things among all consumable records.
*)
procedure TRSConsumable._Setup();
begin
  if Self.IsSetup then
    Exit;

  Self.BankItem := TRSBankItem.Setup(Self.Item);
  Self.Cost := ItemData.GetAverage(Self.Item.GetSingle());
  Self.IsSetup := True;
end;

(*
Consumable.Setup
~~~~~~~~~~~~~~~~
.. pascal::
  procedure TRSConsumable.SetupFood();
  procedure TRSConsumable.SetupPrayer();
  procedure TRSConsumable.SetupEnergy();
  procedure TRSConsumable.SetupAntiPoison();
  procedure TRSConsumable.SetupAntiVenom();
  procedure TRSConsumable.SetupAntiFire();
  procedure TRSConsumable.SetupBoost();

Used internally by consumable handlers/managers to setup the consumable records.
*)
procedure TRSConsumable.SetupFood();
var
  HPLevel: Int32;
begin
  if Self.IsSetup then
    Exit;

  case Self.Item of
    'Shrimps', 'Cooked chicken', 'Cooked meat':
      Self.Points := 3;
    'Sardine', 'Cake(1..3)', 'Edible seaweed':
      Self.Points := 4;
    'Bread', 'Herring', 'Chocolate cake(1..3)', 'Guthix rest(1..4)':
      Self.Points := 5;
    'Mackerel', 'Choc-ice', 'Meat pie(2)', 'Meat pie(1)':
      Self.Points := 6;
    'Trout', 'Cod', 'Plain pizza(1..2)', 'Apple pie(1..2)':
      Self.Points := 7;
    'Pike', 'Roast beast meat', 'Meat pizza(1..2)', 'Admiral pie(1..2)':
      Self.Points := 8;
    'Pineapple punch', 'Salmon', 'Anchovy pizza(1..2)':
      Self.Points := 9;
    'Tuna', 'Dragonfruit pie(1..2)':
      Self.Points := 10;
    'Jug of wine', 'Rainbow fish', 'Stew', 'Banana stew', 'Pineapple pizza(1..2)',
    'Summer pie(1..2)', 'Wild pie(1..2)':
      Self.Points := 11;
    'Lobster':
      Self.Points := 12;
    'Bass':
      Self.Points := 13;
    'Swordfish', 'Potato with butter':
      Self.Points := 14;
    'Tangled toad''s legs', 'Chocolate bomb':
      Self.Points := 15;
    'Potato with cheese', 'Monkfish':
      Self.Points := 16;
    'Cooked karambwan':
      Self.Points := 18;
    'Curry', 'Ugthanki kebab':
      Self.Points := 19;
    'Mushroom potato', 'Shark':
      Self.Points := 20;
    'Sea turtle':
      Self.Points := 21;
    'Manta ray', 'Tuna potato', 'Dark crab':
      Self.Points := 22;
    'Anglerfish':
      begin
        HPLevel := Stats.GetLevel(ERSSkill.HitPoints);
        case HPLevel of
          10..24: Self.Points := Floor(HPLevel * 0.1) + 2;
          25..49: Self.Points := Floor(HPLevel * 0.1) + 4;
          50..74: Self.Points := Floor(HPLevel * 0.1) + 6;
          75..92: Self.Points := Floor(HPLevel * 0.1) + 8;
          93..99: Self.Points := Floor(HPLevel * 0.1) + 13;
        end;
      end;
    'Saradomin brew(1..4)':
      Self.Points := Floor(Stats.GetLevel(ERSSkill.HitPoints) * 0.15) + 2;
  end;

  Self._Setup();
end;

procedure TRSConsumable.SetupPrayer();
begin
  if Self.IsSetup then
    Exit;

  case Self.Item of
    'Prayer potion(1..4)':
      Self.Points := Floor((Stats.GetLevel(ERSSkill.PRAYER) * 0.25) + 7);
    'Super restore(1..4)':
      Self.Points := Floor((Stats.GetLevel(ERSSkill.PRAYER) * 0.25) + 8);
    'Sanfew serum(1..4)':
      Self.Points := Floor((Stats.GetLevel(ERSSkill.PRAYER) * 0.3) + 4);
    'Zamorak brew(1..4)':
      Self.Points := Floor(Stats.GetLevel(ERSSkill.PRAYER) * 0.1);
    'Jangerberries':
      Self.Points := 1;
  end;

  Self._Setup();
end;

procedure TRSConsumable.SetupEnergy();
begin
  if Self.IsSetup then
    Exit;

  case Self.Item of
    'White tree fruit':
      Self.Points := 5;
    'Winter sq''irkjuice':
      Self.Points := 10;
    'Spring sq''irkjuice':
      Self.Points := 20;
    'Autumn sq''irkjuice':
      Self.Points := 30;
    'Summer sq''irkjuice':
      Self.Points := 40;
    'Bandages':
      Self.Points := 30;
    'Guthix rest(1..4)':
      Self.Points := 5;
    'Papaya fruit':
      Self.Points := 5;
    'Energy potion(1..4)':
      Self.Points := 10;
    'Purple sweets':
      Self.Points := 10;
    'Summer pie(1..2)':
      Self.Points := 10;
    'Super energy(1..4)':
      Self.Points := 20;

    'Stamina potion(1..4)':
      begin
        Self.Points := 20;
        Self.Timer := 2 * ONE_MINUTE;
      end;

    'Strange fruit':
      Self.Points := 30;
    'Mint cake':
      Self.Points := 50;
    'Gout tuber':
      Self.Points := 50;
  end;

  Self._Setup();
end;

procedure TRSConsumable.SetupAntiPoison();
begin
  if Self.IsSetup then
    Exit;

  case Self.Item of
    'Sanfew serum(1..4)':
      Self.Timer := 6 * ONE_MINUTE;
    'Anti-venom+(1..4)':
      Self.Timer := 15 * ONE_MINUTE;
    'Anti-venom(1..4)':
      Self.Timer := 12 * ONE_MINUTE;
    'Antidote++(1..4)':
      Self.Timer := 12 * ONE_MINUTE;
    'Antidote+(1..4)':
      Self.Timer := 9 * ONE_MINUTE;
    'Superantipoison(1..4)':
      Self.Timer := 6 * ONE_MINUTE;
    'Antipoison(1..4)':
      Self.Timer := 90 * ONE_SECOND;
  end;

  Self._Setup();
end;

procedure TRSConsumable.SetupAntiVenom();
begin
  if Self.IsSetup then
    Exit;

  case Self.Item of
    'Anti-venom+(1..4)':
      Self.Timer := 3 * ONE_MINUTE;
    'Anti-venom(1..4)':
      Self.Timer := 18 * ONE_SECOND;
  end;

  Self._Setup();
end;

procedure TRSConsumable.SetupAntiFire();
begin
  if Self.IsSetup then
    Exit;

  case Self.Item of
    'Antifire potion(1..4)', 'Extended super antifire(1..4)':
      Self.Timer := 6 * ONE_MINUTE;
    'Super antifire potion(1..4)':
      Self.Timer := 3 * ONE_MINUTE;
    'Extended antifire(1..4)':
      Self.Timer := 12 * ONE_MINUTE;
  end;

  Self._Setup();
end;

procedure TRSConsumable.SetupBoost();
var
  str: String;
begin
  if Self.IsSetup then
    Exit;

  str := ToStr(Self.Item);
  if str.Contains('ivine ') then
    Self.Timer := (5 * ONE_MINUTE) + (3 * ONE_SECOND)
  else
    Self.Timer := 8 * ONE_MINUTE;

  Self._Setup();
end;


(*
ConsumableArray.Contains()
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSConsumableArray.Contains(Value: TRSItem): Boolean;

Wrapper method to check if a TRSConsumableArray has the specified TRSItem in one of it's TRSConsumables.
*)
function TRSConsumableArray.Contains(value: TRSItem): Boolean;
var
  c: TRSConsumable;
begin
  for c in Self do
    if c.Item = value then
      Exit(True);
end;

(*
ConsumableArray.Reversed()
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSConsumableArray.Reversed(): TRSConsumableArray;

Reverses the order of the TRSConsumableArray.
*)
function TRSConsumableArray.Reversed(): TRSConsumableArray;
var
  lo:PtrUInt;
  p,r:^TRSConsumable;
begin
  if (Length(Self) = 0) then
    Exit();

  SetLength(Result,length(self));
  p := @Self[high(self)];
  r := @Result[0];

  lo := PtrUInt(@Self[0]);
  while (lo<=PtrUInt(p)) do
  begin
    r^ := p^;
    dec(p);
    inc(r);
  end;
end;

(*
var TotalConsumableCost
~~~~~~~~~~~~~~~~~~~~~~~
  Global TotalConsumableCost variable used to track the amount of money spent in by consuming consumables.
*)
var
  TotalConsumableCost: Int32;
